apply from: "gradle/dependencies.gradle"

buildscript {
    ext {
        versions = [
                kotlin_plugin: "1.2.31",
        ]
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_plugin"
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}


allprojects { subproj ->
    apply plugin: "kotlin"
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = 'fi.vm.yti.taxgen'

    repositories {
        //mavenLocal()
        mavenCentral()
        jcenter()
    }

	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
		kotlinOptions {
			jvmTarget = '1.8'
			apiVersion = '1.1'
			languageVersion = '1.1'
		}
	}

    test {
        useJUnitPlatform()

        beforeTest { TestDescriptor descriptor ->
            logger.lifecycle("==> ${descriptor.className}: ${descriptor.name}")
        }
    }
}


configure(rootProject) {
	description = 'YTI XBRL Taxonomy Generator'

	jar.enabled = false
	uploadArchives.enabled = false
}


subprojects { subproj ->

    configurations {
        shadowed
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
        main.compileClasspath += configurations.shadowed
        test.runtimeClasspath += configurations.shadowed
    }
}

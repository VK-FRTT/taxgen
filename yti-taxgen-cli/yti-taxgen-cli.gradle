description = 'Stand-alone command line application for executing taxonomy generation from console'


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("com.github.jengelman.gradle.plugins:shadow:${shadowVersion}")
    }
}


apply plugin: 'com.github.johnrengelman.shadow'


dependencies {
    shadowed libs.kotlinStdLib
    shadowed libs.kotlinReflect
    shadowed libs.okHttp3
    shadowed(project(':yti-taxgen-commons'))
    shadowed(project(':yti-taxgen-data-point-meta-model'))
    shadowed(project(':yti-taxgen-ycl-input-parser'))
    shadowed(project(':yti-taxgen-fixta-generator'))

    testCompile libs.junitJupiterApi
    testCompile libs.junitJupiterParams
    testCompile libs.assertjCore

    testRuntimeOnly libs.junitJupiterEngine
}


jar {
    manifest {
        attributes 'Main-Class': 'fi.vm.yti.taxgen.cli.MainKt'
    }
}


jar.enabled = false
jar.dependsOn shadowJar


task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}


shadowJar {
    classifier = null
    configurations = [project.configurations.shadowed]
}


artifacts {
    archives shadowJar
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'fi.vm.yti.taxgen'
            artifactId 'yti-taxgen-cli'
            version '0.1-SNAPSHOT'

            pom.withXml {
                asNode().appendNode('description', description)
            }
            artifact jar
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
